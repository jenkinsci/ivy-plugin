plugins {
    id 'org.jenkins-ci.jpi' version '0.47.0'
}

group = 'org.jenkins-ci.plugins'
description = 'This plugin adds Ivy support to Jenkins'

jenkinsPlugin {
    coreVersion = '2.319.3'

    shortName = 'ivy'
    displayName = 'Ivy Plugin'

    url = 'https://wiki.jenkins.io/display/JENKINS/Ivy+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/ivy-plugin'

    disabledTestInjection = true // seems to fail (erroneously!) on recent cores

    developers {
        developer {
            id 'hibou'
        }
        developer {
            id 'martinficker'
            name 'Martin Ficker'
        }
        developer {
            id 'jmetcalf'
        }
        developer {
            id 'tbingaman'
            name 'Timothy Bingaman'
        }
        developer {
            id 'gboissinot'
            name 'Gregory Boissinot'
        }
        developer {
            id 'arothian'
            name 'Kevin Formsma'
        }
    }
}

sourceCompatibility = '1.8'

compileJava {
    options.encoding = 'UTF-8'
}

java {
    registerFeature('nant') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    implementation 'io.jenkins.plugins:commons-httpclient3-api:3.1-3'
    implementation 'org.jenkins-ci.plugins:config-file-provider:3.10.0'
    implementation 'org.jenkins-ci.plugins:ant:481.v7b_09e538fcca'
    implementation 'org.jenkins-ci.plugins:junit:1.54.3'

    nantImplementation 'org.jvnet.hudson.plugins:nant:1.4.1'

    testImplementation 'io.jenkins:configuration-as-code:1559.v38a_b_2e3b_6b_b_7'
    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:1868.v03b_7c6673e2e'

    implementation 'uk.com.robust-it:cloning:1.9.12'
    implementation 'org.apache.ivy:ivy:2.5.0'
    implementation 'com.jcraft:jsch:0.1.55'
    implementation('org.apache.commons:commons-vfs2:2.2') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

wrapper {
    gradleVersion = '7.4.2'
}
